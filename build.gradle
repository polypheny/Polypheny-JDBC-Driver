plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.9'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.freefair.lombok' version '8.11'
    id 'com.google.protobuf' version '0.9.4'
}

group "org.polypheny"

description = "A standards-compliant JDBC driver for Polypheny."

def versionMajor = 2
def versionMinor = 4
def versionQualifier = "SNAPSHOT"
version = versionMajor + "." + versionMinor + (versionQualifier != '' ? "-" + versionQualifier : '')


repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"


// Allow to disable toolchain setting and use the java available on the system. Important for CI pipelines to test different
// java versions.
if (!hasProperty('disableToolchain')) {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
} else {
    println "Disabled toolchain"
    // Without this, it fails for Java 21
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

java {
    withJavadocJar()
    withSourcesJar()
}

def protobufVersion = '4.28.2'


dependencies {
    // Prism API files (protobuf files), needs to be implementation due to the prism-api-version.properties
    implementation group: 'org.polypheny', name: 'prism', version: '1.9'

    // Protobuf
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion

    // Apache Commons Lang
    implementation group: "org.apache.commons", name: "commons-lang3", version: '3.18.0'

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'


    // --- Test Compile ---
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.11.4'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "io.freefair.lombok"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.google.protobuf"

// compile protos on build
build.dependsOn(generateProto)
delombok.dependsOn(generateProto)
sourcesJar.dependsOn(generateProto)
compileJava.dependsOn(generateProto)
processResources.dependsOn(extractIncludeProto)

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "build/generated/source/proto/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        proto {
            srcDirs = ["build/extracted-include-protos/main"]
        }
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

/**
 * JARs*/
jar {
    manifest {
        attributes 'Manifest-Version': '1.0'
        attributes 'Copyright': 'The Polypheny Project'
        attributes 'Group': project.group
        attributes 'Name': project.name
        attributes 'Version': project.version
    }
}

/*
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}*/

shadowJar {
    archiveClassifier = ''
    mergeServiceFiles() // merge the META-INF/services/java.sql.Driver files
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class', 'META-INF/services/com.fasterxml.jackson.core.*')
    relocate 'google.protobuf', 'org.polypheny.jdbc.dependency.google.protobuf'
    relocate 'org.slf4j', 'org.polypheny.jdbc.dependency.org.slf4j'
    relocate 'org.apache.commons', 'org.polypheny.jdbc.dependency.org.apache.commons'
    relocate 'com.google', 'org.polypheny.jdbc.dependency.com.google'
    relocate 'javax', 'org.polypheny.jdbc.dependency.javax'
    relocate 'org.checkerframework', 'org.polypheny.jdbc.dependency.org.checkerframework'
    relocate 'org.polypheny.prism', 'org.polypheny.jdbc.dependency.prism' // Prevents Polypheny-DB from using this during tests
}

assemble.dependsOn shadowJar
artifacts {
    //archives jar // regular jar containing only the compiled source
    archives shadowJar // fat jar which additionally contains all dependencies
    archives sourcesJar // jar file containing the java doc files
    archives javadocJar // jar file containing the source files
}

// generate version properties file
task generateVersionProperties {
    def versionPropsFile = file('src/main/resources/polypheny-jdbc-driver-version.properties')
    versionPropsFile.parentFile.mkdirs()

    // Generate version properties file with project version
    versionPropsFile.withWriter { writer ->
        writer.write("version=${version}\n")
        writer.write("major=${versionMajor}\n")
        writer.write("minor=${versionMinor}\n")
        writer.write("qualifier=${versionQualifier}\n")
        writer.write("buildTimestamp=${new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")}")
    }
}

// Ensure the generated version properties file is created before compilation
compileJava.dependsOn generateVersionProperties


javadoc {
    exclude 'org/polypheny/prism/**'
    exclude 'com/google/protobuf/**'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
            artifact source: javadocJar, classifier: 'javadoc', extension: 'jar'
            pom {
                name = 'Polypheny JDBC Driver'
                description = 'A standards-compliant JDBC driver for Polypheny-DB.'
                url = 'https://polypheny.org/'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                scm {
                    url = 'https://github.com/polypheny/Polypheny-JDBC-Driver'
                }
                developers {
                    developer {
                        id = 'polypheny'
                        name = 'Polypheny'
                        email = 'mail@polypheny.org'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/polypheny/polypheny-jdbc-driver"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
//task generatePom(group: 'publishing', dependsOn: "generatePomFileFor${project.name.capitalize()}Publication")

signing {
    required { gradle.taskGraph.hasTask("publish") }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.shadow
}

/**
 * IntelliJ*/
idea {
    project {
        settings {
            copyright {
                useDefault = "ApacheLicense"
                profiles {
                    ApacheLicense {
                        notice = 'Copyright 2019-$today.year The Polypheny Project\n' +
                            '\n' +
                            'Licensed under the Apache License, Version 2.0 (the \"License\");\n' +
                            'you may not use this file except in compliance with the License.\n' +
                            'You may obtain a copy of the License at\n' +
                            '\n' +
                            'http://www.apache.org/licenses/LICENSE-2.0\n' +
                            '\n' +
                            'Unless required by applicable law or agreed to in writing, software\n' +
                            'distributed under the License is distributed on an \"AS IS\" BASIS,\n' +
                            'WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n' +
                            'See the License for the specific language governing permissions and\n' +
                            'limitations under the License.'
                        keyword = "Copyright"
                    }
                }
            }
        }
    }
}
